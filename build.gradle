plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.0'
	id 'io.spring.dependency-management' version '1.1.5'
	id 'org.openapi.generator' version '6.5.0'
}

group = 'com.beusable'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
 	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-web'
 	compileOnly 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'

	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	implementation 'org.openapitools:openapi-generator-gradle-plugin:6.6.0'
	implementation 'io.swagger.core.v3:swagger-annotations:2.2.9'
	implementation "org.springframework.boot:spring-boot-starter-log4j2"
	modules {
		module("org.springframework.boot:spring-boot-starter-logging") {
			replacedBy("org.springframework.boot:spring-boot-starter-log4j2", "Use Log4j2 instead of Logback")
		}
	}
}

tasks.named('test') {
	useJUnitPlatform()
}

openApiGenerate {
	templateDir = "${rootDir}/src/templates"
	generatorName = "spring"
	inputSpec = "$rootDir/src/main/resources/RoomOccupancyApi.yaml"
	outputDir = "${buildDir}/generated/openapi"
	apiPackage = "com.beusable.roomoccupancy.api"
	modelPackage = "com.beusable.roomoccupancy.model"
	globalProperties = [
			apis           : "",
			models         : "",
			supportingFiles: "ApiUtil.java"
	]
	configOptions.putAll(["delegatePattern"               : "true",
						  "dateLibrary"                   : "java8",
						  "additionalModelTypeAnnotations": "@lombok.Builder",
						  "generateApis"                  : "true",
						  "generateApiTests"              : "false",
						  "generateModels"                : "true",
						  "generateModelTests"            : "false",
						  "generateModelDocumentation"    : "false",
						  "generateSupportingFiles"       : "false",
						  "hideGenerationTimestamp"       : "true",
						  "interfaceOnly"                 : "false",
						  "library"                       : "spring-boot",
						  "useSpringBoot3"                : "true",
						  "serializableModel"             : "true",
						  "useBeanValidation"             : "true",
						  "useTags"                       : "true",
						  "implicitHeaders"               : "true",
						  "openApiNullable"               : "false",
						  "oas3"                          : "true"])
}
sourceSets.main.java.srcDirs += "${buildDir}/generated/openapi/src/main/java"
compileJava.dependsOn tasks.openApiGenerate